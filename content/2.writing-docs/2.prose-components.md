---
title: 富文本组件
description: 爷手写的富文本组件的使用说明。
navigation:
  icon: i-lucide-component
---


::::char-dialog{character="荷取" variant="狡黠" size="12rem" offsetX="4"}
想让文档变得更加丰富多样？没关系！这里有一些提前写好的富文本组件，可以帮助写出更直观的文档！
::::

## 富文本组件的书写方法

要使用富文本组件，请在文档中使用 `:: + 组件名 + （内容） + ::` 来书写。

例如：

::code-preview
---
class: "[&>div]:*:my-0"
---
:::tip{class="w-full"}
哇多么好的 **机会** 啊！
:::
#code
```mdc
::tip{class="w-full"}
哇多么好的 **机会** 啊！
::
```
::

其中 `::` 代表是组件的开始与结束,`{class="w-full"}` 代表是组件的属性。

不同的组件有一样的HTML属性，例如 `class` 属性，但同时也有组件特有的属性，具体请参考组件的文档。

除此之外，本项目集成了tailwindcss，所以你可以像使用tailwindcss一样用`class`属性来设置组件的样式。例如：

::code-preview
  :::div{.w-full .h-20 .bg-secondary/20 .rounded-lg .shadow-lg .backdrop-blur-lg .flex .items-center .justify-center}
    哇多么好的 **机会** 啊！
  :::
#code
```mdc
  :::div{.w-full .h-20 .bg-secondary/20 .rounded-lg .shadow-lg .backdrop-blur-lg .flex .items-center .justify-center}
    哇多么好的 **机会** 啊！
  :::
```
::

在一部分组件中，可以自定义多个部分的内容，请使用`#(部分名)`来设置。

::code-preview
  :::code-preview
    ::::tip{class="w-full"}
      代码输出
    ::::
  #code
  ```mdc
  :::tip{class="w-full"}
    代码输出
  :::
  ```
  :::
#code
```mdc
:::code-preview
  ::::tip{class="w-full"}
    代码输出
  ::::
#code
\`\`\`mdc
::::tip{class="w-full"}
  代码输出
::::
\`\`\`
:::
```
::

## 折叠组件

使用 `accordion` 和 `accordion-item` 创建可折叠的内容部分。折叠组件适用于过长的分组信息或者Faq。

::callout
折叠组件在文档模式中会强制展开。
::

::code-preview
---
class: "[&>div]:*:my-0"
---
  :::accordion
    ::::accordion-item
    ---
    icon: i-lucide-circle-help
    label: 点击查看详情
    ---
    Lorem ipsum dolor sit amet consectetur adipisicing elit. Quisquam, quos.
    ::::
  
    ::::accordion-item
    ---
    icon: i-lucide-circle-help
    label: 点击查看棍母
    ---
    
    ::::
  
    ::::accordion-item{icon="i-lucide-circle-help" label="不要打开我"}
    ![!!!](https://media.tenor.com/OPhQDzpEWeoAAAAe/kogasa-tatara-kogasa.png)
    哇袄
    ::::
  :::

#code
```mdc
:::accordion
  ::::accordion-item
  ---
  icon: i-lucide-circle-help
  label: 点击查看详情
  ---
  Lorem ipsum dolor sit amet consectetur adipisicing elit. Quisquam, quos.
  ::::

  ::::accordion-item
  ---
  icon: i-lucide-circle-help
  label: 点击查看棍母
  ---
  
  ::::

  ::::accordion-item{icon="i-lucide-circle-help" label="不要打开我"}
  ![!!!](https://media.tenor.com/OPhQDzpEWeoAAAAe/kogasa-tatara-kogasa.png)
  哇袄
  ::::
:::
```
::

## 图标组件

使用图标组件来插入图标库中的图标。

::callout
所有可使用的图标请参考：[icones.js.org](https://icones.js.org/)
::

::code-preview
  :::icon{name="mdi:yin-yang" size="30" class="text-red animate-spin"}
  :::

#code
```mdc
:icon{name="mdi:yin-yang" size="30" class="text-red animate-spin"}
```
::

## 徽标组件

使用徽标组件来显示状态指示器或标签。徽标适用于突出显示版本号、状态或类别。

::code-preview
---
label: Preview
---
  :::badge
  **v3.0.0-alpha.10**
  :::

#code
```mdc
::badge
**v3.0.0-alpha.10**
::
```
::

## 提示组件

使用提示组件来强调重要的上下文信息，使关键信息突出。

::code-preview
---
class: "[&>div]:*:my-0 [&>div]:*:w-full"
---
  :::callout
  This is a `callout` with full **markdown** support.
  :::

#code
```mdc
::callout
This is a `callout` with full **markdown** support.
::
```
::

::code-preview
  :::div{.flex.flex-col.gap-4.w-full}
    ::::note{.w-full.my-0}
    Here's some additional information for you.
    ::::
  
    ::::tip{.w-full.my-0}
    Here's a helpful suggestion.
    ::::
  
    ::::warning{.w-full.my-0}
    Be careful with this action as it might have unexpected results.
    ::::
  
    ::::caution{.w-full.my-0}
    This action cannot be undone.
    ::::
  :::

#code
```mdc
::note
Here's some additional information.
::

::tip
Here's a helpful suggestion.
::

::warning
Be careful with this action as it might have unexpected results.
::

::caution
This action cannot be undone.
::
```
::

## 卡片组件

使用卡片组件来突出显示内容块。卡片适用于展示功能、资源或相关信息，或者作为链接使用。

::code-preview
---
class: "[&>div]:*:my-0 [&>div]:*:w-full"
---
  :::card
  ---
  icon: i-simple-icons-github
  target: _blank
  title: Dashboard
  to: https://github.com/nuxt-ui-pro/dashboard
  ---
  A dashboard with multi-column layout.
  :::

#code
```mdc
::card
---
title: Dashboard
icon: i-simple-icons-github
to: https://github.com/nuxt-ui-pro/dashboard
target: _blank
---
A dashboard with multi-column layout.
::
```
::

## 卡片组

使用卡片组组件来排列卡片。卡片组适用于显示卡片集合。

::code-preview
  :::card-group{.w-full}
    ::::card
    ---
    icon: i-simple-icons-github
    target: _blank
    title: Dashboard
    to: https://github.com/nuxt-ui-pro/dashboard
    ---
    A dashboard with multi-column layout.
    ::::
  
    ::::card
    ---
    icon: i-simple-icons-github
    target: _blank
    title: SaaS
    to: https://github.com/nuxt-ui-pro/saas
    ---
    A template with landing, pricing, docs and blog.
    ::::
  
    ::::card
    ---
    icon: i-simple-icons-github
    target: _blank
    title: Docs
    to: https://github.com/nuxt-ui-pro/docs
    ---
    A documentation with `@nuxt/content`.
    ::::
  
    ::::card
    ---
    icon: i-simple-icons-github
    target: _blank
    title: Landing
    to: https://github.com/nuxt-ui-pro/landing
    ---
    A landing page you can use as starting point.
    ::::
  :::

#code
```mdc
::card-group

::card
---
title: Dashboard
icon: i-simple-icons-github
to: https://github.com/nuxt-ui-pro/dashboard
target: _blank
---
A dashboard with multi-column layout.
::

::card
---
title: SaaS
icon: i-simple-icons-github
to: https://github.com/nuxt-ui-pro/saas
target: _blank
---
A template with landing, pricing, docs and blog.
::

::card
---
title: Docs
icon: i-simple-icons-github
to: https://github.com/nuxt-ui-pro/docs
target: _blank
---
A documentation with `@nuxt/content`.
::

::card
---
title: Landing
icon: i-simple-icons-github
to: https://github.com/nuxt-ui-pro/landing
target: _blank
---
A landing page you can use as starting point.
::

::
```
::


## API字段

使用字段组件来描述一个特定的数据定义。字段组件适用于记录API参数、组件属性或配置选项。

::code-preview
  :::field{.w-full required name="name" type="string"}
  The `description` can be set as prop or in the default slot with full **markdown** support.
  :::

#code
```mdc
::field{name="name" type="string" required}
The `description` can be set as prop or in the default slot with full **markdown** support.
::
```
::

## API字段组

使用字段组组件来将相关字段组合在一起。适用于组织和结构化文档中的多个相关API字段或属性。

::code-preview
  :::field-group
    ::::field{name="analytics" type="boolean"}
    Default to `false` - Enables analytics for your project (coming soon).
    ::::
  
    ::::field{name="blob" type="boolean"}
    Default to `false` - Enables blob storage to store static assets, such as images, videos and more.
    ::::
  
    ::::field{name="cache" type="boolean"}
    Default to `false` - Enables cache storage to cache your server route responses or functions using Nitro's `cachedEventHandler` and `cachedFunction`
    ::::
  
    ::::field{name="database" type="boolean"}
    Default to `false` - Enables SQL database to store your application's data.
    ::::
  :::

#code
```mdc
::field-group
  ::field{name="analytics" type="boolean"}
    Default to `false` - Enables analytics for your project (coming soon).
  ::

  ::field{name="blob" type="boolean"}
    Default to `false` - Enables blob storage to store static assets, such as images, videos and more.
  ::

  ::field{name="cache" type="boolean"}
    Default to `false` - Enables cache storage to cache your server route responses or functions using Nitro's `cachedEventHandler` and `cachedFunction`
  ::

  ::field{name="database" type="boolean"}
    Default to `false` - Enables SQL database to store your application's data.
  ::
::
```
::


## 标签组件

使用标签组件来组织内容。标签适用于将内容分离成逻辑部分，提高内容的可发现性和组织性。

::code-preview
---
class: "[&>div]:*:my-0"
---
  :::tabs{.w-full}
    ::::tabs-item{icon="i-lucide-code" label="Code"}
    ```mdc
    ::callout
    Lorem velit voluptate ex reprehenderit ullamco et culpa.
    ::
    ```
    ::::
  
    ::::tabs-item{icon="i-lucide-eye" label="Preview"}
      :::::callout
      Lorem velit voluptate ex reprehenderit ullamco et culpa.
      :::::
    ::::
  :::

#code
````mdc
::tabs

:::tabs-item{label="Code" icon="i-lucide-code"}

```mdc
::callout
Lorem velit voluptate ex reprehenderit ullamco et culpa.
::
```

:::

:::tabs-item{label="Preview" icon="i-lucide-eye"}

::::callout
Lorem velit voluptate ex reprehenderit ullamco et culpa.
::::

:::

::
````
::
